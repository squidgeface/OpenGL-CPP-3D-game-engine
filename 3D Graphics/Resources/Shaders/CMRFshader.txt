#version 450 core

in vec3 fragNormal;
in vec3 fragPos;
in vec2 fragTexCoord;

out vec4 color;

uniform vec3 camPos;
uniform sampler2D tex;
uniform samplerCube cubeMap;

uniform float	ambientStr		 = 0.01f;
uniform vec3	ambientColor	= vec3(1.0f, 1.0f, 1.0f);
uniform vec3	lightColor;
uniform vec3	lightPos;
uniform float lightSpecStr = 0.5f;
uniform float shininess = 80.0f;	//Range: 1 - 256
uniform float rimExponent = 64.0f; //lower number = bigger bleed
uniform vec3 rimColor = vec3(0.0f, 0.0f, 255.0f);

void main()
{
//ambient
	vec3 ambient = ambientStr * ambientColor;

//light direction
	vec3 norm = normalize(fragNormal);
	vec3 lightDir = normalize(fragPos - lightPos);

//diffuse coloring
	float diffuseStr = max(dot(norm, -lightDir), 0.0f);
	vec3 diffuse = diffuseStr * lightColor;

//specular highlight
	vec3 negViewDir = normalize(camPos - fragPos);
	vec3 halfwayVec = normalize(-lightDir + negViewDir);
	float spec = pow(max(dot(norm, halfwayVec), 0.0f), shininess);
	vec3 specular = lightSpecStr * spec * lightColor;

//rim lighting
	float rimFactor = 1.0f - dot(norm, negViewDir);
	rimFactor = smoothstep(0.0f, 1.0f, rimFactor);
	rimFactor = pow(rimFactor, rimExponent);
	vec3 rim = rimFactor * rimColor * lightColor;

	
	vec3 viewDir = normalize(fragPos - camPos);
	vec3 reflectDir = reflect(viewDir, norm);

	//color = vec4(texture(cubeMap, reflectDir).rgb, 1.0f);
	color = vec4(mix(vec4(ambient + diffuse + specular + rim, 1.0f) * texture(tex, fragTexCoord), vec4(texture(cubeMap, reflectDir).rgb, 1.0f), 0.9f));
}